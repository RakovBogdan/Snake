class SnakeGame {

	static int xPixelSize;
	static int yPixelSize;
	static int exitButton; //'q' button
	static int leftButton;
	static int rightButton;
	static int upButton;
	static int downButton;
	static int left;
	static int right;
	static int up;
	static int down;

	field Snake snake;
	field int direction;
	field Array grid;

	constructor SnakeGame new() {
		var int snakeLength;
		var int snakeWidth;
		let yPixelSize = 256;
		let xPixelSize = 512;
		let exitButton = 81;
		let leftButton = 130;
		let upButton = 131;
		let rightButton = 132;
		let downButton = 133;
		let left = 1;
		let up = 2;
		let right = 3;
		let down = 4;

		let snakeLength = 5;
		let snakeWidth = 8;
		do initializeGrid(snakeWidth);
		let snake = Snake.new(snakeLength, snakeWidth);
		let direction = right;
		return this;
	} 

	method void initializeGrid(int snakeWidth) {
		var int xGridLength;
		var int yGridLength;
		var int yGridIndex;
		var int xGridIndex;
		var Array xGridRow;
		let xGridLength = xPixelSize / snakeWidth;
		let yGridLength = yPixelSize / snakeWidth;
		let grid = Array.new(yGridLength);

		let yGridIndex = 0;
		while (yGridIndex < yGridLength) {
			let xGridRow = Array.new(xGridLength);
			let xGridIndex = 0;
			while (xGridIndex < xGridLength) {
				let xGridRow[xGridIndex] = false;
				let xGridIndex = xGridIndex + 1; 
			}
			let grid[yGridIndex] = xGridRow;
			let yGridIndex = yGridIndex + 1;
		}

		return;
	}

	method void start() {
		var int currentKeyPressed;
		var boolean exit;

		let exit = false;

		while (~(exit)) {
			do moveSnake();

			let currentKeyPressed = Keyboard.keyPressed();
			if (currentKeyPressed = exitButton) {
				let exit = true;
			}
			do readDirection(currentKeyPressed);
			let exit = checkSnakeNotInMargins();
		}

		return;
	}

	method void moveSnake() {
		if (direction = left) {
			do snake.moveLeft();
		}  
		if (direction = right) {
			do snake.moveRight();
		} 
		if (direction = up) {
			do snake.moveUp();
		}
		if (direction = down) {
			do snake.moveDown();
		}
		do Sys.wait(100);
		return;
	}

	method boolean checkSnakeNotInMargins() {
		var int width;
		var SnakePart head;
		var int headX;
		var int headY;
		var boolean isNotInMargin;

		let head = snake.getHead();
		let width = snake.getSnakeWidth();
		let headX = head.getX();
		let headY = head.getY();
		
		if (direction = right) {
			let isNotInMargin = (headX + width + width - 1) > 511;
		}
		if (direction = left) {
			let isNotInMargin = (headX - width) < 0;
		}
		if (direction = up) {
			let isNotInMargin = (headY - width) < 0;
		}
		if (direction = down) {
			let isNotInMargin = (headY + width + width - 1) > 255;
		}

		return isNotInMargin;
	} 

	method void readDirection(int currentKeyPressed) {
		if ((currentKeyPressed = leftButton) & ~(direction = right)) {
			let direction = left;
		}
		if ((currentKeyPressed = upButton) & ~(direction = down)) {
			let direction = up;
		}
		if ((currentKeyPressed = rightButton) & ~(direction = left)) {
			let direction = right;
		}
		if ((currentKeyPressed = downButton) & ~(direction = up)) {
			let direction = down;
		}
		return;
	}

	method void dispose() {
		do snake.dispose();
		do Memory.deAlloc(this);
		return;
	}
}