class Snake {
	
	field Grid grid;
	field SnakePart head, tail;
	field int snakeLength;

	constructor Snake new(int length, int width) {
		let grid = Grid.new(width);
		let snakeLength = length;
		do createSnake();
		return this;
	}

	method void createSnake() {
		var int partsCreated;
		let head = SnakePart.new(0, 0);
		do grid.drawBlock(0, 0);
		let tail = head;
		let partsCreated = 1;
		
		while(partsCreated < snakeLength) {
			do head.setNext(SnakePart.new(partsCreated, 0));
			do grid.drawBlock(partsCreated, 0);
			let head = head.getNext();
			let partsCreated = partsCreated + 1;
		}

		return;
	}

	method int getLength() {
		return snakeLength;
	}

	method Grid getGrid() {
		return grid;
	}

	method SnakePart getHead() {
		return head;
	}

	method SnakePart createNewSnakePart(int direction) {
		var int headX;
		var int headY;
	
		let headX = head.getXIndex();
		let headY = head.getYIndex();
	
		if (direction = SnakeGame.getRight()) {
			let headX = headX + 1;
		}
		if (direction = SnakeGame.getLeft()) {
			let headX = headX - 1;
		}
		if (direction = SnakeGame.getUp()) {
			let headY = headY - 1;
		}
		if (direction = SnakeGame.getDown()) {
			let headY = headY + 1;
		}
				
		return SnakePart.new(headX, headY);
	}
	
	method void eat(SnakePart newSnakePart) {
		do head.setNext(newSnakePart);
		do grid.drawBlock(newSnakePart.getXIndex(), newSnakePart.getYIndex());
		let head = head.getNext(); 
		let snakeLength = snakeLength + 1;
		return;
	}
	
	method void move(SnakePart newSnakePart) {
		do removeTail();
		do head.setNext(newSnakePart);
		do grid.drawBlock(newSnakePart.getXIndex(), newSnakePart.getYIndex());
		let head = head.getNext(); 
		return;
	}
	
	method void removeTail() {
		var SnakePart tailToRemove;
		let tailToRemove = tail;
		let tail = tail.getNext();
		do grid.eraseBlock(tailToRemove.getXIndex(), tailToRemove.getYIndex());
		do tailToRemove.dispose();
		return;
	}

	method void eraseSnakeFromScreen() {
		var SnakePart partToErase;
		let partToErase = tail;

		while (~(partToErase = null)) {
			do grid.eraseBlock(partToErase.getXIndex(), partToErase.getYIndex());
			let partToErase = partToErase.getNext();
		}

		return;	
	}

	method void drawSnakeOnScreen() {
		var SnakePart partToDraw;
		let partToDraw = tail;

		while (~(partToDraw = null)) {
			do grid.drawBlock(partToDraw.getXIndex(), partToDraw.getYIndex());
			let partToDraw = partToDraw.getNext();
		}
		
		return;	
	}

	method void dispose() {
		while (~(tail = null)) {
			do removeTail();
		}

		do grid.dispose();
		do Memory.deAlloc(this);
		return;
	}
}